[[child_processes]]
== Child processes

There are several ways of creating child processes in babashka. Let's start with
the easiest one.

=== clojure.java.shell

A common way to shell out to another process is via `clojure.java.shell`:

[source,clojure]
----
user=> (require '[clojure.java.shell :refer [sh]])
nil
user=> (sh "ls")
{:exit 0, :out "README.md\ndist\ngh-pages\nscript\nsrc\n", :err ""}
----

As you can see the result of `:out` are the lines of text produced by `ls`. The
`:exit` code was `0` and there was no output on stderr.

[source,clojure]
----
user=> (sh "ls" "foo")
{:exit 1, :out "", :err "ls: foo: No such file or directory\n"}
----

If we invoke `ls` with a non-existing file, there is error output, but not
output on stdout and the `:exit` code is `1`.

For more information on `clojure.java.shell`, read the
https://clojure.github.io/clojure/clojure.java.shell-api.html[API documentation]

=== java.lang.Process

What if we wanted to start a test runner that watches a source directory and
executes tests whenever a file changes? Using `clojure.java.shell/sh` for this
has a few drawbacks:

- We have no control over stopping the watcher process
- We can't see any output from the test runner until the process finishes

Using `java.lang.ProcessBuilder` and `java.lang.Process` gives us the extra
power we need to overcome these limitations.

.test_runner_watch.clj
[source,clojure]
----
#!/usr/bin/env bb

(import java.lang.ProcessBuilder$Redirect)

(defn test-runner-watch []
  (let [cmd ["clojure" "-A:kaocha" "-m" "kaocha.runner" "--watch"] <1>
        pb (doto (ProcessBuilder. cmd) <2>
             (.redirectOutput ProcessBuilder$Redirect/INHERIT)) <3>
        proc (.start pb)] <4>
    (-> (Runtime/getRuntime)
        (.addShutdownHook (Thread. #(.destroy proc)))) <5>
    proc))

(.waitFor (test-runner-watch)) <6>
----

<1> The command and its arguments

<2> Construction of a `Process` is done using `ProcessBuilder`

<3> This sends the output of the child process directly to the parent process's
standard out.

<4> Calling `start` on a `ProcessBuilder` instance starts the process and gives
us a `Process` instance.

<5> This line will destroy the child process before the parent process exits.

<6> `waitFor` waits until the child process ends and returns its exit code. If
we would not include this, our script would end without waiting for the watcher.

When calling the script, we can see output from the test runner while it is running:

[source,shell]
----
$ test_runner_watch.clj
[(.................)(..)]
7 tests, 19 assertions, 0 failures.
----

When we change a file in a source directory, the watcher is triggered and the
tests run again.

What if we want to parse the output of the test runner and do something with it?
Instead of sending the output of the child process to stdout we will have to get
a hold of it. This is possible via https://docs.oracle.com/en/java/javase/11/docs/api/java.base/java/lang/Process.html#getInputStream()[`getInputStream`].


.test_runner_watch.clj
[source,clojure]
----
#!/usr/bin/env bb

(defn test-runner-watch []
  (let [cmd ["clojure" "-A:kaocha"
             "-m" "kaocha.runner" "--watch"
             "--no-color"] <1>
        pb (ProcessBuilder. cmd)
        proc (.start pb)
        out (.getInputStream proc)] <2>
    (-> (Runtime/getRuntime)
        (.addShutdownHook (Thread. #(.destroy proc))))
    {:proc proc
     :out out}))

(require '[clojure.java.io :as io])

(let [runner (test-runner-watch)]
  (with-open [reader (io/reader (:out runner))]
    (binding [*in* reader]
      (loop []
        (when-let [line (read-line)]
          (when-let [[_ tests assertions failures]
                     (re-find #"^(\d+) tests, (\d+) assertions, (\d+) failures" line)] <3>
            (prn {:tests (Integer. tests)
                  :assertions (Integer. assertions)
                  :failures (Integer. failures)}))
          (recur))))))
----

<1> We ask the test runner not to output ANSI color codes which makes parsing the output easier
<2> Instead of forwarding the output of the child process to stdout we now grab it as a `java.lang.InputStream`
<3> Here we parse the output of the test runner and then print it as EDN data

Running this script will now print EDN data instead of text:

[source,shell]
----
$ test_runner_watch.clj
{:tests 7, :failures 0, :assertions 19}
----

// TODO: maybe use line-seq in the above example?
// $ bb '(let [pb (ProcessBuilder. ["yes"]) p (.start pb) out (.getInputStream p)] (take 10 (line-seq (io/reader out))))'
// ("y" "y" "y" "y" "y" "y" "y" "y" "y" "y")

// TODO: interactive program example?

// (let [program ["bash" "-c"
//                "echo \"type something!\"; read input; echo \"you typed: $input\""]
//       pb (doto (ProcessBuilder. program)
//            (.inheritIO))
//       p (.start pb)]
//   (.waitFor p) nil)
// type something!
// 1
// you typed: 1
